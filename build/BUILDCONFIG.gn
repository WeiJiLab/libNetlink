if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

is_linux = host_os == "linux" && current_os == "linux" && target_os == "linux"
is_mac = host_os == "mac" && current_os == "mac" && target_os == "mac"

# All binary targets will get this list of configs by default.
shared_binary_target_configs = [
  "//build:compiler_defaults",
  "//build:cxx_17_standard",
  "//build:c_11_standard",
  "//build:pic",
  "//build:pedantic",
  "//build:compiler_warnings",
  "//build:treat_as_errors"
]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = shared_binary_target_configs
  configs += [ "//build:executable_ldconfig" ]
}
set_defaults("static_library") {
  configs = shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = shared_binary_target_configs
}
set_defaults("source_set") {
  configs = shared_binary_target_configs
}

set_default_toolchain("//build/toolchain/clang")
